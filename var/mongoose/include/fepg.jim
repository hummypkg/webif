#!/mod/bin/jimsh

package require cgi
source /mod/var/mongoose/lib/setup
require epg.class spinner.class altrow epg_search totop

[spinner new {
	text "Loading Now/Next Information..."
	size "1.2em"
	style "margin: 1em;"
	}] start

require epg_popup

set start [clock milliseconds]
set now [clock seconds]
set then $($now + 7200)
set records [epg fetch dump -trange "$now:$then"]
set got [clock milliseconds]

set favlist [epg favlist]

puts {
	<table class=borders>
	<tr>
		<th colspan=3>Channel</th>
		<th>On Now</th>
		<th>On Next</th>
	</tr>
}

proc rsort {v1 v2} {
	set v1s [$v1 get channel_num]
	set v2s [$v2 get channel_num]

	if {$v1s > $v2s } { return 1 }
	if {$v1s == $v2s} {
		set v1t [$v1 get start]
		set v2t [$v2 get start]
		if {$v1t > $v2t} { return 1 }
		if {$v1t == $v2t} { return 0 }
		return -1
	}
	return -1
}

foreach record $records {
	$record get_channel_info
}

set lcn 0
set num 0
foreach record [lsort -command rsort $records] {
	if {$favlist != "" && [$record get service_id] ni $favlist} {
		continue
	}
	set chnum [$record get channel_num]
	if {$chnum == 0} { continue }

	if {$chnum != $lcn} {
		if {$lcn > 0} { puts "</tr>" }
		altrow
		puts "<td>$chnum</td>"
		puts "<td>[$record channel_icon 50]</td>
		    <td>
	    <a href=/cgi-bin/epg_service.jim?service=[$record get service_id]>
		    [$record get channel_name]
		    </a></td>"
		set lcn $chnum
		set num 0
	}
	incr num
	if {$num < 4} {
		catch { puts [$record cell] }
	}
}
if {$lcn > 0} { puts "</tr>" }
puts "</table>"

puts "
	<a href=/cgi-bin/settings.jim>
	<img border=0 height=14 src=/images/421_1_00_CH_Title_2R_Arrow.png>
	Visit settings to change EPG options.
	</a><br>
"

set end [clock milliseconds]
puts "<font class=footnote>
    Retrieved in: [expr [expr $got - $start] / 1000.0] seconds.
    Rendered in: [expr [expr $end - $start] / 1000.0] seconds.
    </font>"

epg cleanup

