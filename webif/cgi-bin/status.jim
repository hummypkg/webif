#!/mod/bin/jimsh

package require cgi
source /mod/webif/lib/setup
require system.class findhsvc epg.class rsv.class

set runmode cli
if {[string match {*jim} $argv0]} { set runmode cgi }

set schedtime 1200 
switch $runmode {
    cgi {
	set type [cgi_get type "full"]
	httpheader
    }
    cli {
	set type "full"
	if {"-X" in $argv} { set schedtime 7200 }
    }
}

if {"-d" in $argv} {
	proc debug {arg} { puts $arg }
} else {
	proc debug {arg} {}
}

if {[catch {set pid [exec pgrep -n humaxtv]}]} {
	puts "Cannot find humaxtv process."
	exit
}

set exts {.ts .mkv .avi .mpg .mpeg .wmv .mp3 .mp4 .mov}

proc get_data {} {
	global pid exts opfile

	set ret {}
	if {[catch {set data \
	    [exec /mod/webif/lib/bin/lsof -X -Fns -p $pid]} msg]} {
		set ret {}
	} else {
		set size 0
		foreach line [split $data "\n"] {
			set typ [string index $line 0]
			switch $typ {
			    s {
				set size [string range $line 1 end]
			    }
			    n {
				if {[string first Video/ $line] == -1 && \
				    [string first /media/ $line] == -1} {
					continue
				}
				regsub -- { \([^\)]+\)$} $line "" line
				set ext [file extension $line]
				if {$ext ni $exts} continue
				set file [string range $line 1 end]
				# Handle chase play (same file open twice
				# and recently written)
				if {[dict exists $ret $file] && \
				    $ext eq ".ts"} {
					set age [expr [clock seconds] - \
					    [file mtime $file]]
					if {$age < 60} {
						set ret($file) -1
					}
				} else  {
					set ret($file) $size
				}
			    }
			}
		}
		if {$opfile ne "" && $opfile ni $ret} {
			set ret($opfile) [file size $opfile]
		}
	}
	return $ret
}

set play 0
set rec 0
set output {}

set opfile ""
if {[file exists "/mod/tmp/webif_auto/.op"} {
	set fp [open "/mod/tmp/webif_auto/.op"]
	lassign [split [string trim [$fp read]] ":"] op opfile
	$fp close
	debug "OP: $op - $opfile"
}

set data {}
if {$type eq "full"} {
	set data [get_data]
}
if {[llength $data]} {
	debug " DATA: ($data)"
	if {![file exists /tmp/.rractive]} {
		sleep 3
		set ndata [get_data]
		debug " NDATA: ($ndata)"
		set rr 0
	} else {
		sleep 1
		set ndata [get_data]
		debug " NDATA: ($ndata)"
		foreach k [array names data] {
			if {![dict exists $ndata $k]} {
				dict unset data $k
			}
		}

		set rr 1
		set recs {}
		loop i 0 2 {
			catch {
				set fd [open "/tmp/.rec$i" r]
				lappend recs "[$fd read -nonewline].ts"
				$fd close
			}
		}
		debug " RECS: $recs"
	}
	foreach file [array names data] {
		set bname [file rootname [file tail $file]]

		if {$file eq $opfile} {
			set mode $op
		} elseif {$data($file) == -1} {
			set mode chase
		} elseif {$rr} {
			if {$file in $recs} {
				set mode rec
			} else {
				set mode play
			}
		} else {
			if {![dict exists $ndata $file]} continue
			if {$ndata($file) > $data($file)} {
				set mode rec
			} else {
				set mode play
			}
		}

		switch $mode {
			chase {
				incr rec
				incr play
				set mode "Chase Playing"
				set icon "745_1_11_Video_3REC_Live.png"
			}
			rec {
				incr rec
				set mode "Recording"
				set icon "745_1_11_Video_1REC.png"
			}
			play {
				incr play
				set mode "Playing"
				set icon "745_1_10_Video_2Live.png"
			}
			dec {
				set mode "Decrypting"
				set icon "/img/decrypt.png "
				append icon "style=\"padding: 0 0.2em 0 0.5em\""
#				set icon "178_1_00_Icon_Lock.png "
#				append icon "style=\"padding: 0 0.2em 0 0.5em\""
			}
			mpg {
				set mode "Extracting MPG"
				set icon "/img/mpg.png "
				append icon "style=\"padding: 0 0.2em 0 0.5em\""
			}
			mp3 {
				set mode "Extracting MP3"
				set icon "/img/mp3.png "
				append icon "style=\"padding: 0 0.2em 0 0.5em\""
			}
			shrink {
				set mode "Shrinking"
				set icon "/img/compress.png "
				append icon "style=\"padding: 0 0.2em 0 0.5em\""
			}
		}

		if {[string first "/" $icon] == -1} {
			set icon "/images/$icon"
		}

		set name [string map {
			"/mnt/hd2/My Video/" ""
			"/media/drive1/Video/" ""
			"/media/" ""
			".ts" ""
		} $file]

		if {$runmode eq "cgi"} {
			lappend output [concat \
			    "<span class=\"va stitem\">\n" \
			    "  <img class=va src=$icon>\n" \
			    "  <span>$mode&nbsp;<i>$name</i></span>\n" \
			    "</span>\n" \
			    ]
		} else {
			lappend output "$mode $name"
		}
	}
}

######################################################################
# Live viewing information

if {![system instandby] && $play < 1} {
	set hsvc [system param CUR_SVC Value USERCONFIG]

	set ff [$rsvdb query "
		select usSvcid as svcid, usLcn as lcn,
		substr(szSvcName, 2) as name
		from channel.TBL_SVC
		where hSvc = $hsvc
		limit 1
	"]

	if {[llength $ff] == 1} {
		lassign [lindex $ff 0] x svcid x lcn x name
		set epgs [epg dbfetch get \
		    -service $svcid \
		    -time [clock seconds]\
		]
		set prog ""
		if {[llength $epgs] == 1} {
			lassign $epgs epg
			set prog "- [$epg get name] ("
			append prog "[clock format [$epg get start] -format %H:%M] - "
			append prog "[clock format $([$epg get start] + [$epg get duration]) -format %H:%M]"
			append prog ") \[[$epg percent]%\]"
		}

		if {$runmode eq "cgi"} {
			lappend output [concat \
			    "<span class=\"va stitem\">\n" \
			    "  [epg channelicon $name 30 \
			    {vertical-align: middle; padding: 0 4px 0 2px}]\n" \
			    "  <span>Watching <i>$lcn: $name $prog</i></span>\n" \
			    "</span>\n" \
			    ]
		} else {
			lappend output "Watching $lcn: $name $prog"
		}
	}
}

######################################################################
# In standby

if {[system instandby]} {
	if {$runmode eq "cgi"} {
		lappend output [concat \
		    "<span class=\"va stitem\">\n" \
		    "  <img class=va width=28 style=\"padding: 0 4px\"" \
		    "src=/img/standby.png>\n" \
		    "  <span>System is in standby.</span>\n" \
		    "</span>" \
		]
	} else {
		lappend output "System is in standby."
	}
}

######################################################################
# Upcoming recordings

set events [rsv list tbl_reservation \
    " where ersvtype = 3 and nsttime - [clock seconds] < $schedtime 
      and nsttime > [clock seconds] "]
foreach event $events {
	if {$runmode eq "cgi"} {
		set icon "175_1_11_Reservation_Record.png"
		lappend output [concat \
		    "<span class=\"va stitem\">\n" \
		    "  <img class=va src=/images/$icon width=20>\n" \
		    "  <span>Will record <i>[$event name]</i>" \
		    " on [$event channel_name] at"\
		    " [clock format [$event get nsttime] -format {%H:%M}]" \
		    "  </span>\n" \
		    "</span>\n" \
	} else {
		lappend output [concat \
		    "Will record '[$event name]' " \
		    "on [$event channel_name] at " \
		    "[clock format [$event get nsttime] -format {%H:%M}]" \
		]
	}
}

######################################################################
# Output

if {[llength $output]} {
	if {$runmode eq "cgi"} {
		puts [join $output "\n<br>\n"]
	} else {
		puts [join $output "\n"]
	}
}

